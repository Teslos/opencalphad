@$ 
@$ Calculate a phase diagram for FCC ordering in the Fe-Ni system
@$ using partition and permutations
@&

set echo

@$ Enter the elements and their reference states
enter element Fe Iron BCC 55.847 0 0 

enter element Ni Nickel FCC 58.69 0 0

@$ These functions describe the end-member energies at Fe3Ni, Fe2Ni2 and FeNi3
@$ respectivly.  The VASP energies relative to pure Fe amd Ni as fcc are:
@$ Fe3Ni1 -0.071689 eV for 1 atom??
@$ Fe2Ni2 -0.138536 eV for 1 atom??
@$ Fe1Ni3 -0.125748 eV for 1 atom??
@$ To modify to J/mol atoms multiply with 96500
@$ bond energy multiplied with 3, 4 and 3 respectively.

enter tp-sym evtoj constant 96500

enter tp-sym GA3B1 fun 1 -0.071689*evtoj;,,,,,
enter tp-sym GA2B2 fun 1 -0.138536*evtoj;,,,,,
enter tp-sym GA1B3 fun 1 -0.125748*evtoj;,,,,,
 
@$ We may have to use some regular solution parameter later
enter tp-sym L0 fun 1 12000; ,,,,,
enter tp-sym L1 fun 1 0; ,,,,,
enter tp-sym L2 fun 1 0; ,,,,,

@$ this is an approximate SRO contribution to the LRO phase.  It is
@$ set to about a quater of the L1_0 ordering energy, 
@$ equal to the Fe-Ni bond energy
enter tp-sym GSRO fun 1 -0.034*evtoj;,,,,,

@$ ==================================================
@$ This is an fcc phase with lro but no explicit sro
@$ described with the sublattice model

enter phase PARTITIONED_FCC 4 .25 Fe NI; .25 Fe NI; .25 Fe NI; .25 Fe NI;

@$ we must set that this has FCC permutations before entering parameters
set phase part bit fcc-perm

@$ we must add disordered set before entering parameters
amend phase part dis 4 yes

enter param G(part,Fe:Fe:Fe:Ni),,GA3B1; 6000 N test
enter param G(part,Fe:Ni:Ni:Ni),,GA1B3; 6000 N test
enter param G(part,Fe:Fe:Ni:Ni),,GA2B2; 6000 N test

enter param G(part,Fe,Ni:Fe,Ni:*:*),,GSRO; 6000 N test

amend biblio test VASP calculation by test;

@$ These are possible disordered parameters
enter param GD(part,Fe,Ni;0),,L0; 6000 N test
enter param GD(part,Fe,Ni;1),,L1; 6000 N test
enter param GD(part,Fe,Ni;2),,L2; 6000 N test


list data ,,


@&

we have to create composition sets manually

@$ this default constitution is Fe3Ni_L12
amend phase part comp-set y , ,
<.2 >.5
<.2 >.5
<.2 >.5
>.5 <.2

@$ this default constitution is FeNi_L10
amend phase part comp-set y , ,
<.2 >.5
<.2 >.5
>.5 <.2
>.5 <.2

@$ this default constitution is FeNi3_L12
amend phase part comp-set y , ,
<.2 >.5
<.2 >.5
<.2 >.5
>.5 <.2

@$ However, the L12 can have max Ni or Fe on any sublattice, there is no
@$ check that it is always the first or last sublattice with the highest
@$ fraction of the minor element.  This should be arranged in todo_after ...


set c t=400 p=1e5 n=1 x(ni)=.6

c e

l r 2


@&

set ax 1 x(ni) 0 1 ,,,,

@$ we have to take very small T steps
set ax 2 t 10 1000 10

map




@$ just the FeNi/FeNi3


set c x(ni)=.9

c e

map
N




plot ,,,,,,,,,,,,,,,,




set c t=300 x(ni)=.4

c e

map
N




plot ,,,,,,,,,,,,,,,,




set inter



